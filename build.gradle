plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.31"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
}

group = "com.github.technoir42"
version = "0.0.1"

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.4.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.4.2"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.4.2"
}

test {
    useJUnitPlatform()
}

tasks.register("packageSources", Jar.class) {
    dependsOn tasks.named("classes")
    from sourceSets.main.allSource
    archiveClassifier = "sources"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact packageSources
        }
    }
}

bintray {
    user = findProperty("BINTRAY_USER")
    key = findProperty("BINTRAY_KEY")
    publish = true

    pkg {
        repo = "maven"
        name = "kotlin-extensions"
        licenses = ["Apache-2.0"]
        vcsUrl = "https://github.com/technoir42/kotlin-extensions.git"
        version {
            name = project.version
        }
        publications = ["maven"]
    }
}
